plugins {
    id 'org.springframework.boot' version '2.1.7.RELEASE'
    id "org.springframework.cloud.contract" version "2.1.2.RELEASE"
    id "com.gorylenko.gradle-git-properties" version "2.0.0"
    id 'java'
    id 'maven-publish'
}

apply plugin: 'io.spring.dependency-management'

group = 'me.fwfurtado'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

ext {
    set('springBootAdminVersion', "2.1.5")
    set('springCloudVersion', "Greenwich.SR2")
}

contracts {

    packageWithBaseClasses = 'me.fwfurtado.contracts.base'

    baseClassMappings {
        baseClassMapping('.*car-command-consumer.*', 'me.fwfurtado.user.details.DetailBase')
    }

}

publishStubsToScm {
    customize {
        contractDependency {
            stringNotation = "${project.group}:${project.name}:${project.version}"
        }

        contractsMode = "REMOTE"

        contractRepository {
            repositoryUrl = "git://git@github.com:fwfurtado/car-sharing-contracts.git"
        }
    }
}

publish.dependsOn('publishStubsToScm')
publishToMavenLocal.dependsOn("publishStubsToScm")

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation 'de.codecentric:spring-boot-admin-starter-client'
    implementation 'org.springframework.cloud:spring-cloud-starter-config'

    implementation 'org.springframework.cloud:spring-cloud-stream'
    implementation 'org.springframework.cloud:spring-cloud-starter-stream-kafka'
    implementation 'org.springframework.cloud:spring-cloud-starter-zipkin'

    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.flywaydb:flyway-core'
    runtimeOnly 'mysql:mysql-connector-java'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
}

dependencyManagement {
    imports {
        mavenBom "de.codecentric:spring-boot-admin-dependencies:${springBootAdminVersion}"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}